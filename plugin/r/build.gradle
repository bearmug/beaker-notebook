/*
 *  Copyright 2014 TWO SIGMA OPEN SOURCE, LLC
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */
apply plugin:'application'
mainClassName = 'com.twosigma.beaker.r.Main'

configurations {
  provided
  compile.extendsFrom provided
}

repositories {
  mavenCentral()
}

dependencies {
  provided project(':plugin:jvm')
  provided group: 'net.rforge', name: 'Rserve', version: '0.6-8.1'
  compile 'org.apache.xmlgraphics:batik-rasterizer:1.8'
  compile 'org.apache.xmlgraphics:batik-codec:1.8'
  compile 'org.apache.xmlgraphics:xmlgraphics-commons:2.0.1'
}

compileJava {
  options.compilerArgs << '-Xlint:deprecation'
  options.compilerArgs << '-Xlint:unchecked'
  options.encoding = 'UTF-8'
}

// Fix command-length issue in windows startscript
task pathingJar(type: Jar) {
  appendix = 'pathing'
  manifest {
    attributes("Class-Path": configurations.runtime.collect { it.getName() }.join(' ') + ' ' + jar.archiveName )
  }
}

applicationDistribution.from(pathingJar) {
  into "lib"
}

startScripts {
  defaultJvmOpts=['-Djava.awt.headless=' + 'true']
  doLast {
    // Remove too-long-classpath and use wildcard ( works for java 6 and above only)
    windowsScript.text = windowsScript.text.replaceAll('set CLASSPATH=.*', 'set CLASSPATH=.;%APP_HOME%/lib/*')
  }
}

if (hasProperty('evalPluginDir')) {
  installDist.into new File(evalPluginDir, 'r')
}

if (hasProperty('includeDepsInJar')) {
  build.dependsOn jar
  jar {
    // need to remove META-INF/*.SF to use this.
    from {
       (configurations.compile - configurations.provided).collect { it.isDirectory() ? it : zipTree(it).matching{exclude{it.path.contains('META-INF')} } }
    } {
      exclude "META-INF/*.SF"
      exclude "META-INF/*.DSA"
      exclude "META-INF/*.RSA"
    }
  }
}

task realclean
realclean.dependsOn(clean)
